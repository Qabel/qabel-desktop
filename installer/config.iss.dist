#define AppName "Qabel Desktop Client"
#define AppVersion "{version}"
#define AppPublisher "Qabel GmbH"
#define AppURL "https://qabel.de"
#define AppExeName "QabelDesktop.exe"
#define SetupIcon "logo.ico"

[Setup]
AppId={{2271156D-AF5F-491E-B9D0-FE78708D1464}
AppName={#AppName}
AppVersion={#AppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#AppPublisher}
AppPublisherURL={#AppURL}
AppSupportURL={#AppURL}
AppUpdatesURL={#AppURL}
DefaultDirName={pf}\{#AppName}
DefaultGroupName={#AppName}
AllowNoIcons=yes
OutputDir=.
OutputBaseFilename=qabel-desktop-client-{version}-beta
Compression=lzma
SolidCompression=yes
PrivilegesRequired=admin
SetupIconFile={#SetupIcon}
UsePreviousAppDir=True
AppCopyright={#AppPublisher}
ArchitecturesAllowed=x64
ArchitecturesInstallIn64BitMode=x64

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"

[CustomMessages]
english.licenseFile=en_license.txt
german.licenseFile=de_license.txt
english.termsFile=en_terms.txt
german.termsFile=de_terms.txt
english.policyFile=de_policy.txt
german.policyFile=de_policy.txt

english.DeleteConfig=Do you want to delete the configuration file?%n%nIf you delete the configuration file, your identities will be deleted as well and you will not be able to access your uploaded data anymore. Export your identities before you delete the configuration file, if you would still like to be able to access your data (e.g. from a different device or anytime in the future).%n%nTo export your identity, click on "Export identity" under "Identities" in your Qabel client.%n%nYour identity contains your private key. Store it safely and don't pass it on to third parties.
german.DeleteConfig=Wollen Sie die Konfigurationsdatei löschen?%n%nWenn Sie die Konfigurationsdatei löschen, werden auch Ihre Identitäten entfernt und Sie haben somit keinen Zugriff mehr auf Ihre hochgeladenen Daten. Exportieren Sie Ihre Identitäten, bevor Sie die Konfigurationsdatei löschen, wenn Sie weiterhin die Möglichkeit haben wollen, auf Ihre Daten zuzugreifen (z. B. von einem anderen Gerät oder irgendwann in der Zukunft).%n%nZum Export Ihrer Identität klicken Sie auf "Identität exportieren" unter "Identität" in Ihrem Qabel-Client.%n%nIhre Identität enthält Ihren privaten Schlüssel. Bewahren Sie die Datei also sicher auf und geben Sie sie nicht an Dritte weiter.

english.Terms=Terms of Service
german.Terms=Allgemeine Geschäftsbedingungen
english.TermsHeader=Please read the following Terms of Service. You must accept the Terms of Service before continuing with the installation.
german.TermsHeader=Bitte lesen Sie folgende Bestimmungen
english.TermsAccept=I accept the Terms of Service
german.TermsAccept=Ich akzeptiere die allgemeinen Geschäftsbedingungen

english.Policy=Privacy Policy
german.Policy=Datenschutzerklärung
english.PolicyHeader=Please read the following Privacy Policy. You must accept the Privacy Policy before continuing with the installation.
german.PolicyHeader=Bitte lesen Sie folgende Datenschutzerklärung
english.PolicyAccept=I accept the Privacy Policy
german.PolicyAccept=Ich akzeptiere die Datenschutzerklärung
english.LoadingTerms=Loading legal notices...
german.LoadingTerms=Lade Rechtliches...

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "QabelDesktop.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "dist\lib\*"; DestDir: "{app}\lib"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "jre\*"; DestDir: "{app}\jre"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: content\*; DestDir: {app}\content; Flags: ignoreversion

[Icons]
Name: "{group}\{#AppName}"; Filename: "{app}\{#AppExeName}"
Name: "{group}\{cm:UninstallProgram,{#AppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#AppName}"; Filename: "{app}\{#AppExeName}"; Tasks: desktopicon
Name: "{userstartup}\{#AppName}"; Filename: "{app}\{#AppExeName}"

[Run]
Filename: "{app}\{#AppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(AppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent


[Code]
procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
    if CurUninstallStep = usUninstall then begin
        if MsgBox(ExpandConstant('{cm:DeleteConfig}'), mbConfirmation, MB_YESNO) = IDYES
            then begin
                DeleteFile(ExpandConstant('{%HOMEPATH}\.qabel\db.sqlite'));
            end;
    end;
end;

var
	TermsAccept: TNewCheckBox;
    PolicyAccept: TNewCheckBox;
    TermsPage: TOutputMsgMemoWizardPage;
    PolicyPage: TOutputMsgMemoWizardPage;
    LoaderPage: TOutputProgressWizardPage;
    Loaded: Boolean;

procedure TermsOpenPage(Sender: TWizardPage);
begin
	WizardForm.NextButton.Enabled := TermsAccept.Checked;
end;
procedure TermsAccepted(Sender: TObject);
begin
	WizardForm.NextButton.Enabled := TermsAccept.Checked;
end;
procedure PolicyOpenPage(Sender: TWizardPage);
begin
	WizardForm.NextButton.Enabled := PolicyAccept.Checked;
end;
procedure PolicyAccepted(Sender: TObject);
begin
	WizardForm.NextButton.Enabled := PolicyAccept.Checked;
end;

procedure CreateCustomWizardAgreementPage(TPage: TOutputMsgMemoWizardPage; TAccept: TNewCheckBox; acceptLabel: AnsiString; agreementFilename: AnsiString);
var
	TText: AnsiString;
begin
    TPage.RichEditViewer.Height := ScaleY(148);

    TAccept.Parent := TPage.Surface;
    TAccept.Caption := acceptLabel;
    TAccept.Width := TPage.RichEditViewer.Width;
    TAccept.Top := TPage.Surface.ClientHeight - ScaleY(41);
    TAccept.Left := TPage.RichEditViewer.Left;

	ExtractTemporaryFile(agreementFilename);
	LoadStringFromFile(ExpandConstant('{tmp}/' + agreementFilename), TText);
	TPage.RichEditViewer.RTFText := TText;
end;

procedure TermsInitializeWizard();
begin
    TermsPage := CreateOutputMsgMemoPage(wpLicense, CustomMessage('Terms'), SetupMessage(msgLicenseLabel), CustomMessage('TermsHeader'), '33');
    TermsAccept := TNewCheckBox.Create(TermsPage);
    CreateCustomWizardAgreementPage(TermsPage, TermsAccept, CustomMessage('TermsAccept'), ExpandConstant('{cm:termsFile}'));
    TermsPage.OnActivate := @TermsOpenPage;
    TermsAccept.OnClick := @TermsAccepted;
    TermsAccept.Checked := false;
end;
procedure PolicyInitializeWizard();
begin
    PolicyPage := CreateOutputMsgMemoPage(wpLicense, CustomMessage('Policy'), SetupMessage(msgLicenseLabel), CustomMessage('PolicyHeader'), '33');
    PolicyAccept := TNewCheckBox.Create(PolicyPage);
    CreateCustomWizardAgreementPage(PolicyPage, PolicyAccept, CustomMessage('PolicyAccept'), ExpandConstant('{cm:policyFile}'));
    PolicyPage.OnActivate := @PolicyOpenPage;
    PolicyAccept.OnClick := @PolicyAccepted;
    PolicyAccept.Checked := false;
end;


procedure InitTerms(Sender: TWizardPage);
begin
    try
        LoaderPage.setProgress(0,2);
        TermsInitializeWizard;
        LoaderPage.setProgress(1,2);
        PolicyInitializeWizard;
        LoaderPage.setProgress(2,2);
    finally
        LoaderPage.Hide();
    end;
end;

procedure InitializeWizard();
begin
    Loaded := False;
    LoaderPage := CreateOutputProgressPage(CustomMessage('LoadingTerms'), CustomMessage('LoadingTerms'));
    LoaderPage.OnActivate := @InitTerms;
end;

procedure CurPageChanged(CurPageID: Integer);
begin
    if Loaded = False then begin
        Loaded := True;
        LoaderPage.Show();
    end;
end;
