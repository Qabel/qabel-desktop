apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'c'

mainClassName = "de.qabel.desktop.DesktopClient"
version = '0.1'
def platform = "${System.properties['os.name'].toLowerCase()}_${System.properties['os.arch']}"

repositories {
    mavenCentral()
    jcenter()
    flatDir {
        dir 'libs'
    }
}

sourceSets {
    guiTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/test-gui/java')
        }
        resources.srcDir file('src/test-gui/resources')
    }
}

configurations {
    guiTestCompile.extendsFrom testCompile
    guiTestRuntime.extendsFrom testRuntime
    compile {
        description = 'compile classpath'
        transitive = true
    }
    runtime {
        extendsFrom compile
    }
}

startScripts {
    classpath += configurations.compile
    doLast{
        unixScript.text = unixScript.text.replace('DEFAULT_JVM_OPTS=""', 'DEFAULT_JVM_OPTS="-Djava.library.path=\\\$APP_HOME/lib/"')
    }
}

jar.doFirst{
	manifest {
		attributes 'Implementation-Title': 'Qabel Desktop', 'Implementation-Version': version
		attributes 'Main-Class': 'de.qabel.desktop.DesktopClient'
	    attributes 'Class-Path': configurations.runtime.collect { it.getName() }.join(' ')
	}
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Qabel Desktop', 'Implementation-Version': version
        attributes 'Main-Class': 'de.qabel.desktop.DesktopClient'
        attributes 'Class-Path': configurations.runtime.collect { it.getName() }.join(' ')
    }

    baseName = project.name + '-all-' + platform
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar

}
distZip() {
    baseName = project.name + '-' + platform

    from (configurations.compile) {
        into baseName + '-' + version + '/lib/'
    }
    from ("qabel-core/build/binaries/curve25519SharedLibrary/$platform/") {
        into baseName + '-' + version + '/lib/'
    }
}

dependencies {
    testCompile 'junit:junit:4.11'
    testCompile group: 'org.meanbean', name: 'meanbean', version: '2.+'
    testCompile 'org.testfx:testfx-core:4.0.+'
    testCompile 'org.testfx:testfx-junit:4.0.+'

    compile 'commons-cli:commons-cli:1.2'
    compile 'com.amazonaws:aws-java-sdk:1.10.32'
    // see https://github.com/AdamBien/afterburner.fx/pull/54 //compile 'com.airhacks:afterburner.fx:1.6.2' // Apache 2.0
    compile 'javax.inject:javax.inject:1'
    compile name: 'afterburner'

    compile project(":qabel-core")
}

task copyToLib(type: Copy) {
  	from configurations.runtime
    into "$buildDir/libs"  
}

classes {   
	 doLast {
        copyToLib.execute()
    }
}

task guiTest(type: Test) {
    testClassesDir = sourceSets.guiTest.output.classesDir
    classpath = sourceSets.guiTest.runtimeClasspath
    outputs.upToDateWhen { false }
}
check.dependsOn guiTest
guiTest.mustRunAfter test

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

tasks.withType(Test) {
    systemProperty "java.library.path", "./qabel-core/build/binaries/curve25519SharedLibrary/$platform/"

    reports.html.destination = file("${reporting.baseDir}/${name}")
}
run {
    systemProperty "java.library.path", "./qabel-core/build/binaries/curve25519SharedLibrary/$platform/"
}
